// vim: ft=groovy
buildscript {
    dependencies {
        classpath 'commons-io:commons-io:2.4'
    }
    repositories {
        mavenCentral()
    }
}

import org.apache.commons.io.FileUtils

repositories {
    mavenLocal()
    maven { url 'http://repo.jenkins-ci.org/releases/' }
    jcenter()
    mavenCentral()
    maven { url 'https://raw.github.com/seanly/mvnrepository/jenkins-plugins/' }
}

configurations {
    jenkinsWar
    jenkinsPlugins
}

dependencies {
    jenkinsWar(group: 'org.jenkins-ci.main', name: 'jenkins-war', version: project.ext.jenkinsVersion, ext: 'war')
}
apply from: 'plugins.gradle'

defaultTasks 'runJenkins'

task runJenkins << {
    File jenkinsHome = prepareJenkinsHome()
    File jenkinsWar = prepareJenkinsWar()

    def jenkinsPort = prepareJenkinsPort()
    def dslProjectRoot = project.projectDir

    def stdout = new ByteArrayOutputStream()

    exec {
        executable = 'java'
        args = [
              "-DJENKINS_HOME=${jenkinsHome.absolutePath}",
              "-DDSL_PROJECT_ROOT=${dslProjectRoot.absolutePath}",
              '-jar', jenkinsWar.absolutePath,
              "--webroot=${jenkinsHome.absolutePath}/war",
              "--httpPort=${jenkinsPort}"
        ]
        standardOutput = stdout
    }

    println "Jenkin Start: \n$stdout"
}

private String prepareJenkinsPort() {
     def jenkinsPort = '8080'
     if (project.hasProperty('jenkinsPort')) {
          jenkinsPort = project.ext.jenkinsPort
     }
     return jenkinsPort
}

private File prepareJenkinsWar() {
    def configuration = project.configurations.getByName('jenkinsWar')
    def files = configuration.resolve()
    def war = files.first()
    war
}

private File prepareJenkinsHome() {
    File jenkinsHome = getJenkinsHome()
    //createOrUpdateBaseConfig(jenkinsHome)
    preparePluginsDir(jenkinsHome)
    return jenkinsHome
}

private File getJenkinsHome() {
    def jenkinsHome = new File(System.getProperty('user.home'), "${project.name}_jenkins_home")
    if (project.hasProperty('localJenkinsHome')) {
        jenkinsHome = new File(project.ext.localJenkinsHome)
    }
    return jenkinsHome
}

private createOrUpdateBaseConfig(File jenkinsHome) {
    FileUtils.copyDirectory(new File('./jenkins-bootstrap/'), jenkinsHome.absoluteFile)
}

private void preparePluginsDir(File jenkinsHome) {
    def pluginsDir = new File(jenkinsHome, 'plugins/')

    // copied from gradle-jpi-plugin
    // create new configuration with plugin dependencies, ignoring the (jar) extension to get the HPI/JPI files
    Configuration plugins = project.configurations.create('plugins')
    project.configurations.getByName('jenkinsPlugins').dependencies.each {
        project.dependencies.add(plugins.name, "${it.group}:${it.name}:${it.version}")
    }

    // copy the resolved HPI/JPI files to the plugins directory
    plugins.resolvedConfiguration.resolvedArtifacts.findAll { it.extension in ['hpi', 'jpi'] }.each {
        GFileUtils.copyFile(it.file, new File(pluginsDir, "${it.name}.${it.extension}"))
    }

    // pin particular plugins
    new File(pluginsDir.absolutePath + '/credentials.jpi.pinned').createNewFile()
}
